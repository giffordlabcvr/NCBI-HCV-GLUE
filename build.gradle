apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'org.hidetake.ssh'

project.ext.set("glueVersion", "0.1.113")

remotes {
  gluehome_aws {
    host = '52.16.113.119'
    user = 'ubuntu'
    identity = file('/Users/joshsinger/.ssh/gluehome-aws.pem')
  }
  
}

repositories {
	mavenLocal()
}

configurations {
	gluetoolsCore
}

dependencies {
    gluetoolsCore group: 'cvr.ac.uk', name: 'gluetools-core', version: project.glueVersion, transitive: false
}


buildscript {
  repositories {
    // for ssh-deploy stuff
    jcenter()
    // for axion-release
    mavenCentral()
  }
  dependencies {
    classpath group: 'org.hidetake', name: 'gradle-ssh-plugin', version: '1.1.3'
    classpath group: 'commons-io', name: 'commons-io', version: '1.3.2'
    classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.3.2'
  }
}

task copyGluetoolsJar(type: Copy) {
    from configurations.gluetoolsCore
    into "$buildDir/runtimeJars"
    rename('gluetools-core-'+project.glueVersion+'.jar', 'gluetools-core.jar')
}



task updateHcvGlueDB(type: JavaExec, dependsOn: 'copyGluetoolsJar') {
	classpath "build/runtimeJars/gluetools-core.jar"
	main 'uk.ac.gla.cvr.gluetools.core.console.Console'
	args '-c', 'local-gluetools-config.xml', '-i', 'run', 'file', 'ncbiHcvUpdate.glue'
}

task dumpHcvGlueDB(type: Exec, dependsOn: 'updateHcvGlueDB') {
	standardOutput = new FileOutputStream(new File('/tmp/hcv_glue.sql'))
	commandLine '/usr/local/mysql/bin/mysqldump', '-u', 'hcvglue', '--password=hcvglue', 'HCV_GLUE'
}

task zipHcvGlueDB(type: Exec, dependsOn: 'dumpHcvGlueDB') {
	commandLine '/usr/bin/gzip', '-f', '/tmp/hcv_glue.sql'
}

task deployHcvGlueDbToGlueHomeAWS(dependsOn: 'zipHcvGlueDB') {
  doLast {
	  println "Uploading HCV-GLUE DB"
	  ssh.run {
	    session(remotes.gluehome_aws) {
	 	  put from: '/tmp/hcv_glue.sql.gz', into: '/tmp/hcv_glue.sql.gz'
	      execute 'echo "drop database HCV_GLUE; create database HCV_GLUE character set UTF8;" | mysql -u hcvglue --password=hcvglue HCV_GLUE'
	      execute 'gunzip -c /tmp/hcv_glue.sql.gz | mysql -u hcvglue --password=hcvglue HCV_GLUE'
	      execute 'rm /tmp/hcv_glue.sql.gz'
	    }
	  }
  }
}

scmVersion {
	hooks {
        pre 'fileUpdate', [file: 'glue/ncbiHcvProjectSettings.glue', pattern: {v, c -> /set setting extension-version $v/}, replacement: {v, c -> "set setting extension-version $v"}]
		pre 'commit', {v, p -> "Release version $v"}
    }

    scmVersion {
        versionCreator 'versionWithBranch'
	    repository {
	        customKey = file('/Users/joshsinger/.ssh/github_id_rsa')
	    }
	    tag {
	        prefix = 'NCBI-HCV-GLUE'
	    }
    }

	project.version = scmVersion.version
}








